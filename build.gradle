buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "4.1.+", changing: true
        classpath group: "org.spongepowered", name: "mixingradle", version: "0.7-SNAPSHOT"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "maven-publish"
apply plugin: "org.spongepowered.mixin"

mixin {
    add sourceSets.main, "${project.modId}.refmap.json"
}

configurations {
    shade
}

version = "${project.minecraft}-${project.modVersion}"
group = "com.minecraftabnormals"
archivesBaseName = project.modId

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = "1.8"

minecraft {
    mappings channel: project.mappingsChannel, version: project.mappings
    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
    runs {
        client {
            workingDirectory project.file("run")
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            property "forge.logging.console.level", "debug"
            arg "-mixin.config=" + project.modId + ".mixins.json"
            mods {
                abnormals_core {
                    source sourceSets.main
                }
                ac_test {
                    source sourceSets.test
                }
            }
        }
        server {
            workingDirectory project.file("run")
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            property "forge.logging.console.level", "debug"
            arg "-mixin.config=" + project.modId + ".mixins.json"
            mods {
                abnormals_core {
                    source sourceSets.main
                }
                ac_test {
                    source sourceSets.test
                }
            }
        }
        data {
            workingDirectory project.file("run")
            property "forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP"
            property "forge.logging.console.level", "debug"
            property "mixin.env.disableRefMap", "true"
            args "--mod", project.modId, "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources")
            mods {
                abnormals_core {
                    source sourceSets.main
                }
                ac_test {
                    source sourceSets.test
                }
            }
        }
    }
}

task deobfJar(type: Jar) {
    description = "Creates a JAR containing the non-obfuscated compiled code."
    from sourceSets.main.output
    classifier = "deobf"
}

artifacts {
    archives deobfJar
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.minecraft}-${project.forge}"

    compileOnly fg.deobf("com.github.Ocelot5836:Sonar:4cc95e6:api")
    runtimeOnly fg.deobf("com.github.Ocelot5836:Sonar:4cc95e6")
    shade "com.github.Ocelot5836:Sonar:4cc95e6"

    annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'
}

repositories {
    maven { url = "https://jitpack.io" }
}

jar {
    classifier = "slim"
    manifest {
        attributes([
                "Specification-Title"     : project.modName,
                "Specification-Vendor"    : "Team Abnormals",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.modName,
                "Implementation-Version"  : project.modVersion,
                "Implementation-Vendor"   : "Team Abnormals",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : project.modId + ".mixins.json"
        ])
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'io.github.ocelot', 'com.minecraftabnormals.abnormals_core.lib.sonar'
    classifier = null
}

reobf {
    shadowJar {}
}

artifacts {
    archives jar
    archives shadowJar
}

build.dependsOn reobfShadowJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }
    repositories {
        if(project.hasProperty("jacksonMavenUser") && project.hasProperty("jacksonMavenPass")) {
            maven {
                url "https://maven.jaackson.me"
                credentials {
                    username = project.properties.jacksonMavenUser
                    password = project.properties.jacksonMavenPass
                }
            }
        }
    }
}